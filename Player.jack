// Author:    Daniel Edwards
// File name: Player.jack

/**
 * This represents the player. It's relatively basic, mostly just in charge
 * of actually drawing the player and tracking their position. It doesn't
 * attempt to check if the desired positions are valid; that's up to the
 * caller.
 */
class Player {
	static int SIZE;
	static int COL_COUNT;	// Used because rawDraw is dumb.

	field int row;
	field int col;

	constructor Player new(int r, int c) {
		let COL_COUNT = 32;
		let SIZE = 16;

		let row = r;
		let col = c;

		do draw();

		return this;
	}

	method void dispose() {
		//do clear();

		return;
	}

	method int getRow() {
		return row;
	}

	method int getCol() {
		return col;
	}

	method void move(int newCol, int newRow) {
		do clear();
		let col = newCol;
		let row = newRow;
		do draw();

		return;
	}


	method void draw() {
		do Player.rawDraw((row * SIZE * COL_COUNT) + (col));
		return;
	}

	method void clear() {
		
		var int x1;
		var int y1;
		var int x2;
		var int y2;
	
		let x1 = col * SIZE;
		let x2 = x1 + SIZE - 1;

		let y1 = row * SIZE;
		let y2 = y1 + SIZE - 1;

		do Screen.setColor(false);
		do Screen.drawRectangle(x1, y1, x2, y2);
		do Screen.setColor(true);

		return;
	}


	function void rawDraw(int location) {
		var int memAddress;
		let memAddress = 16384+location;
		do Memory.poke(memAddress+0, 0);
		do Memory.poke(memAddress+32, 0);
		do Memory.poke(memAddress+64, 2016);
		do Memory.poke(memAddress+96, 4080);
		do Memory.poke(memAddress+128, 3504);
		do Memory.poke(memAddress+160, 3504);
		do Memory.poke(memAddress+192, 2016);
		do Memory.poke(memAddress+224, 0);
		do Memory.poke(memAddress+256, 960);
		do Memory.poke(memAddress+288, 2016);
		do Memory.poke(memAddress+320, 2016);
		do Memory.poke(memAddress+352, 2016);
		do Memory.poke(memAddress+384, 2016);
		do Memory.poke(memAddress+416, 1632);
		do Memory.poke(memAddress+448, 3696);
		do Memory.poke(memAddress+480, 0);
		return;
	}
}
